'OHRRPGCE CUSTOM - Misc unsorted routines
'(C) Copyright 1997-2005 James Paige and Hamster Republic Productions
'Please read LICENSE.txt for GPL License details and disclaimer of liability
'See README.txt for code docs and apologies for crappyness of this code ;)
'
#ifdef LANG_DEPRECATED
 #define __langtok #lang
 __langtok "deprecated"
 OPTION STATIC
 OPTION EXPLICIT
#endif

#include "config.bi"
#include "const.bi"
#include "udts.bi"
#include "custom_udts.bi"
#include "allmodex.bi"
#include "common.bi"
#include "loading.bi"
#include "customsubs.bi"
#include "slices.bi"
#include "cglobals.bi"

#include "uiconst.bi"
#include "scrconst.bi"

#include "flexmenu.bi"

'Menus.bas
DECLARE FUNCTION dissolve_type_caption(n as integer) as string

'Defined in this file:

DECLARE SUB enemy_edit_add_new (recbuf() as integer, byref recindex as integer, preview_box as Slice Ptr)

DECLARE SUB individual_formation_editor ()
DECLARE SUB formation_set_editor ()
DECLARE sub drawformsprites(form as Formation, egraphics() as GraphicPair, byval slot as integer)
DECLARE sub formpics(ename() as string, form as Formation, egraphics() as GraphicPair)

DECLARE SUB load_item_names (item_strings() as string)
DECLARE FUNCTION item_attack_name(n as integer) as string
DECLARE SUB generate_item_edit_menu (menu() as string, itembuf() as integer, csr as integer, pt as integer, item_name as string, info_string as string, equip_types() as string, byref box_preview as string)

DECLARE SUB item_editor_equipbits(itembuf() as integer)
DECLARE SUB item_editor_elementals(itembuf() as integer)
DECLARE SUB item_editor_init_new(itembuf() as integer)

DECLARE SUB enforce_hero_data_limits(her as HeroDef)
DECLARE SUB update_hero_appearance_menu(byref st as HeroEditState, menu() as string, her as HeroDef)
DECLARE SUB update_hero_preview_pics(byref st as HeroEditState, her as HeroDef)
DECLARE SUB animate_hero_preview(byref st as HeroEditState)
DECLARE SUB clear_hero_preview_pics(byref st as HeroEditState)
DECLARE SUB draw_hero_preview(st as HeroEditState, her as HeroDef)

DECLARE SUB hero_editor
DECLARE SUB hero_editor_add_new (st as HeroEditState, her as HeroDef, byref hero_id as integer)
DECLARE SUB hero_editor_load_hero (st as HeroEditState, her as HeroDef, byval hero_id as integer)
DECLARE SUB hero_editor_stats_menu (her as HeroDef)
DECLARE SUB hero_editor_spell_lists_toplevel (her as HeroDef)
DECLARE SUB hero_editor_edit_spell_list (her as HeroDef, byval listnum as integer)
DECLARE SUB hero_editor_spell_list_names (her as HeroDef)
DECLARE SUB update_hero_tags_menu (byref hero as HeroDef, menu() as string)
DECLARE SUB hero_editor_tags (byval hero_id as integer, byref hero as HeroDef)
DECLARE SUB hero_editor_appearance (byref st as HeroEditState, byref her as HeroDef)
DECLARE SUB hero_editor_equipment_list (byval hero_id as integer, byref her as HeroDef)
DECLARE SUB hero_editor_equipbits (byval hero_id as integer, byval equip_type as integer)
DECLARE SUB hero_editor_elementals(byref her as HeroDef)
DECLARE SUB hero_editor_edit_spell_list_slot (slot as SpellList)
DECLARE SUB hero_editor_battle_menu (her as HeroDef)

SUB clearallpages
clearpage 0 'UPDATE as of fbc v0.20
clearpage 1 'I re-checked a loop version of this. It's still bigger.
clearpage 2 'Sure, it really doesn't matter, but just saying...
clearpage 3 '~Mike
END SUB

SUB update_enemy_editor_for_elementals(recbuf() as integer, caption() as string, byval EnCapElemResist as integer)
 FOR i as integer = 0 TO gen(genNumElements) - 1
  caption(EnCapElemResist + i) = format_percent(DeSerSingle(recbuf(), 239 + i*2))
 NEXT
END SUB

SUB enemydata

DIM elementnames() as string
getelementnames elementnames()

clearallpages

'-------------------------------------------------------------------------

'--bitsets
DIM ebit(64) as string

FOR i as integer = 32 TO 53
 ebit(i) = "" 'preferable to be blank, so we can hide it
NEXT i
ebit(54) = "Harmed by Cure"
ebit(55) = "MP Idiot"
ebit(56) = "Boss"
ebit(57) = "Unescapable"
ebit(58) = "Die Without Boss"
ebit(59) = "Flee instead of Die"
ebit(60) = "Untargetable by Enemies"
ebit(61) = "Untargetable by Heros"
ebit(62) = "Win battle even if alive"
ebit(63) = "Never flinch when attacked"
ebit(64) = "Ignored for ""Alone"" AI"

'-------------------------------------------------------------------------

'--record buffer
DIM recbuf(dimbinsize(binDT1)) as integer

CONST EnDatName = 0' to 16
CONST EnDatStealAvail = 17
CONST EnDatStealItem = 18
CONST EnDatStealItemP = 19
CONST EnDatStealRItem = 20
CONST EnDatStealRItemP = 21
CONST EnDatDissolve = 22
CONST EnDatDissolveTime = 23
CONST EnDatDeathSFX = 24
CONST EnDatCursorX = 25
CONST EnDatCursorY = 26
'27 to 52 unused
CONST EnDatPic = 53
CONST EnDatPal = 54
CONST EnDatPicSize = 55
CONST EnDatGold = 56
CONST EnDatExp = 57
CONST EnDatItem = 58
CONST EnDatItemP = 59
CONST EnDatRareItem = 60
CONST EnDatRareItemP = 61
CONST EnDatStat = 62' to 73
CONST EnDatBitset = 74' to 78
CONST EnDatSpawnDeath = 79
CONST EnDatSpawnNEDeath = 80
CONST EnDatSpawnAlone = 81
CONST EnDatSpawnNEHit = 82
CONST EnDatSpawnElement = 83' to 90
CONST EnDatSpawnNum = 91
CONST EnDatAtkNormal = 92' to 96
CONST EnDatAtkDesp = 97'   to 101
CONST EnDatAtkAlone = 102' to 106
CONST EnDatElemCtr = 107' to 114
CONST EnDatStatCtr = 115' to 126
CONST EnDatElemCtr2 = 127' to 182
CONST EnDatSpawnElement2 = 183' to 238
CONST EnDatElemResist = 239' to 366
CONST EnDatAtkBequest = 367

'-------------------------------------------------------------------------

DIM capindex as integer = 0
REDIM caption(-1 TO -1) as string
DIM max(26) as integer
DIM min(26) as integer
'Limit 0 is not used

CONST EnLimPic = 1
max(EnLimPic) = gen(genMaxEnemy1Pic) 'or 28 or 29. Must be updated!

CONST EnLimUInt = 2
max(EnLimUInt) = 32767

CONST EnLimPicSize = 3
max(EnLimPicSize) = 2
DIM EnCapPicSize as integer = capindex
addcaption caption(), capindex, "Small 34x34"
addcaption caption(), capindex, "Medium 50x50"
addcaption caption(), capindex, "Big 80x80"

CONST EnLimItem = 4
max(EnLimItem) = gen(genMaxItem)

CONST EnLimPercent = 5
max(EnLimPercent) = 100

CONST EnLimStat = 6' to 17
FOR i as integer = 0 TO 1:  max(EnLimStat + i) = 32767: NEXT i ' HP and MP
FOR i as integer = 2 TO 8:  max(EnLimStat + i) = 999:   NEXT i ' regular stats
FOR i as integer = 9 TO 10: max(EnLimStat + i) = 100:   NEXT i ' focus, counter
max(EnLimStat + 11) = 10        ' max hits

CONST EnLimSpawn = 18
max(EnLimSpawn) = gen(genMaxEnemy) + 1 'must be updated!

CONST EnLimSpawnNum = 19
max(EnLimSpawnNum) = 8

CONST EnLimAtk = 20
max(EnLimAtk) = gen(genMaxAttack) + 1

CONST EnLimStr16 = 21
max(EnLimStr16) = 16

CONST EnLimStealAvail = 22
min(EnLimStealAvail) = -1
max(EnLimStealAvail) = 1
addcaption caption(), capindex, "Disabled"
DIM EnCapStealAvail as integer = capindex
addcaption caption(), capindex, "Only one"
addcaption caption(), capindex, "Unlimited"

CONST EnLimPal16 = 23
max(EnLimPal16) = 32767
min(EnLimPal16) = -1

CONST EnLimDissolve = 24
min(EnLimDissolve) = 0
max(EnLimDissolve) = dissolveTypeMax + 1
DIM EnCapDissolve as integer = capindex
addcaption caption(), capindex, "Global Default"
FOR i as integer = 0 TO dissolveTypeMax
 addcaption caption(), capindex, dissolve_type_caption(i)
NEXT

CONST EnLimDissolveTime = 25
min(EnLimDissolveTime) = 0
max(EnLimDissolveTime) = 99

CONST EnLimDeathSFX = 26
min(EnLimDeathSFX) = -1
max(EnLimDeathSFX) = gen(genMaxSFX) + 1

DIM EnCapElemResist as integer = capindex
FOR i as integer = 0 TO gen(genNumElements) - 1
 addcaption caption(), capindex, ""  '--updated in update_enemy_editor_for_elementals
NEXT

'--next limit 27, remeber to update dim!

'-------------------------------------------------------------------------
'--menu content
DIM menu(260) as string
DIM menutype(260) as integer
DIM menuoff(260) as integer
DIM menulimits(260) as integer

CONST EnMenuBackAct = 0
menu(EnMenuBackAct) = "Previous Menu"
menutype(EnMenuBackAct) = 1

CONST EnMenuChooseAct = 1
menu(EnMenuChooseAct) = "Enemy"
menutype(EnMenuChooseAct) = 5

CONST EnMenuName = 2
menu(EnMenuName) = "Name:"
menutype(EnMenuName) = 4
menuoff(EnMenuName) = EnDatName
menulimits(EnMenuName) = EnLimStr16

CONST EnMenuAppearAct = 3
menu(EnMenuAppearAct) = "Appearance & Sounds..."
menutype(EnMenuAppearAct) = 1

CONST EnMenuRewardAct = 4
menu(EnMenuRewardAct) = "Rewards..."
menutype(EnMenuRewardAct) = 1

CONST EnMenuStatAct = 5
menu(EnMenuStatAct) = "Stats..."
menutype(EnMenuStatAct) = 1

CONST EnMenuBitsetAct = 6
menu(EnMenuBitsetAct) = "Bitsets..."
menutype(EnMenuBitsetAct) = 1

CONST EnMenuSpawnAct = 7
menu(EnMenuSpawnAct) = "Spawning..."
menutype(EnMenuSpawnAct) = 1

CONST EnMenuAtkAct = 8
menu(EnMenuAtkAct) = "Attacks..."
menutype(EnMenuAtkAct) = 1

CONST EnMenuPic = 9
menu(EnMenuPic) = "Picture:"
menutype(EnMenuPic) = 0
menuoff(EnMenuPic) = EnDatPic
menulimits(EnMenuPic) = EnLimPic

CONST EnMenuPal = 10
menu(EnMenuPal) = "Palette:"
menutype(EnMenuPal) = 12
menuoff(EnMenuPal) = EnDatPal
menulimits(EnMenuPal) = EnLimPal16

CONST EnMenuPicSize = 11
menu(EnMenuPicSize) = "Picture Size:"
menutype(EnMenuPicSize) = 2000 + EnCapPicSize
menuoff(EnMenuPicSize) = EnDatPicSize
menulimits(EnMenuPicSize) = EnLimPicSize

CONST EnMenuGold = 12
menu(EnMenuGold) = "Gold:"
menutype(EnMenuGold) = 0
menuoff(EnMenuGold) = EnDatGold
menulimits(EnMenuGold) = EnLimUInt

CONST EnMenuExp = 13
menu(EnMenuExp) = "Experience Points:"
menutype(EnMenuExp) = 0
menuoff(EnMenuExp) = EnDatExp
menulimits(EnMenuExp) = EnLimUInt

CONST EnMenuItem = 14
menu(EnMenuItem) = "Item:"
menutype(EnMenuItem) = 8
menuoff(EnMenuItem) = EnDatItem
menulimits(EnMenuItem) = EnLimItem

CONST EnMenuItemP = 15
menu(EnMenuItemP) = "Item%:"
menutype(EnMenuItemP) = 0
menuoff(EnMenuItemP) = EnDatItemP
menulimits(EnMenuItemP) = EnLimPercent

CONST EnMenuRareItem = 16
menu(EnMenuRareItem) = "Rare Item:"
menutype(EnMenuRareItem) = 8
menuoff(EnMenuRareItem) = EnDatRareItem
menulimits(EnMenuRareItem) = EnLimItem

CONST EnMenuRareItemP = 17
menu(EnMenuRareItemP) = "Rare Item%:"
menutype(EnMenuRareItemP) = 0
menuoff(EnMenuRareItemP) = EnDatRareItemP
menulimits(EnMenuRareItemP) = EnLimPercent

CONST EnMenuStat = 18' to 29
FOR i as integer = 0 TO 11
 menu(EnMenuStat + i) = statnames(i) + ":"
 menutype(EnMenuStat + i) = 0
 menuoff(EnMenuStat + i) = EnDatStat + i
 menulimits(EnMenuStat + i) = EnLimStat + i
NEXT i
menutype(EnMenuStat + 8) = 15 'Speed should show turn-time estimate

CONST EnMenuSpawnDeath = 30
menu(EnMenuSpawnDeath) = "Spawn on Death:"
menutype(EnMenuSpawnDeath) = 9
menuoff(EnMenuSpawnDeath) = EnDatSpawnDeath
menulimits(EnMenuSpawnDeath) = EnLimSpawn

CONST EnMenuSpawnNEDeath = 31
menu(EnMenuSpawnNEDeath) = "on Non-Elemental Death:"
menutype(EnMenuSpawnNEDeath) = 9
menuoff(EnMenuSpawnNEDeath) = EnDatSpawnNEDeath
menulimits(EnMenuSpawnNEDeath) = EnLimSpawn

CONST EnMenuSpawnAlone = 32
menu(EnMenuSpawnAlone) = "Spawn When Alone:"
menutype(EnMenuSpawnAlone) = 9
menuoff(EnMenuSpawnAlone) = EnDatSpawnAlone
menulimits(EnMenuSpawnAlone) = EnLimSpawn

CONST EnMenuSpawnNEHit = 33
menu(EnMenuSpawnNEHit) = "on Non-Elemental Hit:"
menutype(EnMenuSpawnNEHit) = 9
menuoff(EnMenuSpawnNEHit) = EnDatSpawnNEHit
menulimits(EnMenuSpawnNEHit) = EnLimSpawn

CONST EnMenuSpawnElement = 34' to 93
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuSpawnElement + i) = "on " & elementnames(i) & " Hit:"
 menutype(EnMenuSpawnElement + i) = 9
 IF i < 8 THEN
  menuoff(EnMenuSpawnElement + i) = EnDatSpawnElement + i
 ELSE
  menuoff(EnMenuSpawnElement + i) = EnDatSpawnElement2 + (i - 8)
 END IF
 menulimits(EnMenuSpawnElement + i) = EnLimSpawn
NEXT i

CONST EnMenuSpawnNum = 94
menu(EnMenuSpawnNum) = "How Many to Spawn:"
menutype(EnMenuSpawnNum) = 0
menuoff(EnMenuSpawnNum) = EnDatSpawnNum
menulimits(EnMenuSpawnNum) = EnLimSpawnNum

CONST EnMenuAtkNormal = 95' to 99
FOR i as integer = 0 TO 4
 menu(EnMenuAtkNormal + i) = "Normal:"
 menutype(EnMenuAtkNormal + i) = 7
 menuoff(EnMenuAtkNormal + i) = EnDatAtkNormal + i
 menulimits(EnMenuAtkNormal + i) = EnLimAtk
NEXT i

CONST EnMenuAtkDesp = 100' to 104
FOR i as integer = 0 TO 4
 menu(EnMenuAtkDesp + i) = "Desperation:"
 menutype(EnMenuAtkDesp + i) = 7
 menuoff(EnMenuAtkDesp + i) = EnDatAtkDesp + i
 menulimits(EnMenuAtkDesp + i) = EnLimAtk
NEXT i

CONST EnMenuAtkAlone = 105' to 109
FOR i as integer = 0 TO 4
 menu(EnMenuAtkAlone + i) = "Alone:"
 menutype(EnMenuAtkAlone + i) = 7
 menuoff(EnMenuAtkAlone + i) = EnDatAtkAlone + i
 menulimits(EnMenuAtkAlone + i) = EnLimAtk
NEXT i

CONST EnMenuStealItem = 110
menu(EnMenuStealItem) = "Stealable Item:"
menutype(EnMenuStealItem) = 8
menuoff(EnMenuStealItem) = EnDatStealItem
menulimits(EnMenuStealItem) = EnLimItem

CONST EnMenuStealRItem = 111
menu(EnMenuStealRItem) = "Rare Stealable Item:"
menutype(EnMenuStealRItem) = 8
menuoff(EnMenuStealRItem) = EnDatStealRItem
menulimits(EnMenuStealRItem) = EnLimItem

CONST EnMenuStealItemP = 112
menu(EnMenuStealItemP) = "Steal Rate%:"
menutype(EnMenuStealItemP) = 0
menuoff(EnMenuStealItemP) = EnDatStealItemP
menulimits(EnMenuStealItemP) = EnLimPercent

CONST EnMenuStealRItemP = 113
menu(EnMenuStealRItemP) = "Rare Steal Rate%:"
menutype(EnMenuStealRItemP) = 0
menuoff(EnMenuStealRItemP) = EnDatStealRItemP
menulimits(EnMenuStealRItemP) = EnLimPercent

CONST EnMenuStealAvail = 114
menu(EnMenuStealAvail) = "Steal Availability:"
menutype(EnMenuStealAvail) = 2000 + EnCapStealAvail
menuoff(EnMenuStealAvail) = EnDatStealAvail
menulimits(EnMenuStealAvail) = EnLimStealAvail

CONST EnMenuDissolve = 115
menu(EnMenuDissolve) = "Death Animation:"
menutype(EnMenuDissolve) = 2000 + EnCapDissolve
menuoff(EnMenuDissolve) = EnDatDissolve
menulimits(EnMenuDissolve) = EnLimDissolve

CONST EnMenuDissolveTime = 116
menu(EnMenuDissolveTime) = "Death Animation ticks:"
menutype(EnMenuDissolveTime) = 13
menuoff(EnMenuDissolveTime) = EnDatDissolveTime
menulimits(EnMenuDissolveTime) = EnLimDissolveTime

CONST EnMenuDeathSFX = 117
menu(EnMenuDeathSFX) = "Death Sound Effect:"
menutype(EnMenuDeathSFX) = 14
menuoff(EnMenuDeathSFX) = EnDatDeathSFX
menulimits(EnMenuDeathSFX) = EnLimDeathSFX

CONST EnMenuCursorOffset = 118
menu(EnMenuCursorOffset) = "Cursor Offset..."
menutype(EnMenuCursorOffset) = 1

CONST EnMenuElemCtr = 119' to 182
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuElemCtr + i) = "Counter element " & elementnames(i) & ":"
 menutype(EnMenuElemCtr + i) = 7
 IF i < 8 THEN
  menuoff(EnMenuElemCtr + i) = EnDatElemCtr + i
 ELSE
  menuoff(EnMenuElemCtr + i) = EnDatElemCtr2 + (i - 8)
 END IF
 menulimits(EnMenuElemCtr + i) = EnLimAtk
NEXT i

CONST EnMenuStatCtr = 183' to 194
FOR i as integer = 0 TO 11
 menu(EnMenuStatCtr + i) = "Counter damage to " & statnames(i) & ":"
 menutype(EnMenuStatCtr + i) = 7
 menuoff(EnMenuStatCtr + i) = EnDatStatCtr + i
 menulimits(EnMenuStatCtr + i) = EnLimAtk
NEXT i

CONST EnMenuElementalsAct = 195
menu(EnMenuElementalsAct) = "Elemental Resistances..."
menutype(EnMenuElementalsAct) = 1

CONST EnMenuElemDmg = 196' to 259
FOR i as integer = 0 TO gen(genNumElements) - 1
 menu(EnMenuElemDmg + i) = "Damage from " + rpad(elementnames(i), " ", 15) + ":"
 menutype(EnMenuElemDmg + i) = 5000 + EnCapElemResist + i  'percent_grabber
 menuoff(EnMenuElemDmg + i) = 239 + i*2 
NEXT

CONST EnMenuAtkBequest = 260
menu(EnMenuAtkBequest) = "On-Death Bequest Attack:"
menutype(EnMenuAtkBequest) = 7
menuoff(EnMenuAtkBequest) = EnDatAtkBequest
menulimits(EnMenuAtkBequest) = EnLimAtk

'-------------------------------------------------------------------------
'--menu structure
'WARNING: make these big enough to hold atkMenu when genNumElements is maxed out
DIM workmenu(92) as integer
DIM dispmenu(92) as string
DIM state as MenuState
state.size = 24

DIM mainMenu(9) as integer
mainMenu(0) = EnMenuBackAct
mainMenu(1) = EnMenuChooseAct
mainMenu(2) = EnMenuName
mainMenu(3) = EnMenuAppearAct
mainMenu(4) = EnMenuRewardAct
mainMenu(5) = EnMenuStatAct
mainMenu(6) = EnMenuBitsetAct
mainMenu(7) = EnMenuElementalsAct
mainMenu(8) = EnMenuSpawnAct
mainMenu(9) = EnMenuAtkAct

DIM appearMenu(7) as integer
appearMenu(0) = EnMenuBackAct
appearMenu(1) = EnMenuPicSize
appearMenu(2) = EnMenuPic
appearMenu(3) = EnMenuPal
appearMenu(4) = EnMenuDissolve
appearMenu(5) = EnMenuDissolveTime
appearMenu(6) = EnMenuDeathSFX
appearMenu(7) = EnMenuCursorOffset

DIM rewardMenu(11) as integer
rewardMenu(0) = EnMenuBackAct
rewardMenu(1) = EnMenuGold
rewardMenu(2) = EnMenuExp
rewardMenu(3) = EnMenuItem
rewardMenu(4) = EnMenuItemP
rewardMenu(5) = EnMenuRareItem
rewardMenu(6) = EnMenuRareItemP
rewardMenu(7) = EnMenuStealAvail
rewardMenu(8) = EnMenuStealItem
rewardMenu(9) = EnMenuStealItemP
rewardMenu(10) = EnMenuStealRItem
rewardMenu(11) = EnMenuStealRItemP

DIM statMenu(12) as integer
statMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO 11
 statMenu(1 + i) = EnMenuStat + i
NEXT i

DIM spawnMenu(5 + gen(genNumElements)) as integer
spawnMenu(0) = EnMenuBackAct
spawnMenu(1) = EnMenuSpawnNum
spawnMenu(2) = EnMenuSpawnDeath
spawnMenu(3) = EnMenuSpawnNEDeath
spawnMenu(4) = EnMenuSpawnAlone
spawnMenu(5) = EnMenuSpawnNEHit
FOR i as integer = 0 TO gen(genNumElements) - 1
 spawnMenu(6 + i) = EnMenuSpawnElement + i
NEXT i

DIM atkMenu(28 + gen(genNumElements)) as integer
atkMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO 4
 atkMenu(1 + i) = EnMenuAtkNormal + i
 atkMenu(6 + i) = EnMenuAtkDesp + i
 atkMenu(11 + i) = EnMenuAtkAlone + i
NEXT i
atkMenu(16) = EnMenuAtkBequest
FOR i as integer = 0 TO gen(genNumElements) - 1
 atkMenu(17 + i) = EnMenuElemCtr + i
NEXT i
FOR i as integer = 0 TO 11
 atkMenu(17 + gen(genNumElements) + i) = EnMenuStatCtr + i
NEXT i

DIM elementalMenu(gen(genNumElements)) as integer
elementalMenu(0) = EnMenuBackAct
FOR i as integer = 0 TO gen(genNumElements) - 1
 elementalMenu(1 + i) = EnMenuElemDmg + i
NEXT i

DIM helpkey as string = "enemy"

'--Create the box that holds the preview
DIM preview_box as Slice Ptr
preview_box = NewSliceOfType(slRectangle)
ChangeRectangleSlice preview_box, ,uilook(uiDisabledItem), uilook(uiMenuItem), , transOpaque
'--Align the box in the bottom right
WITH *preview_box
 .X = -8
 .Y = -8
 .Width = 82
 .Height = 82
 .AnchorHoriz = 2
 .AlignHoriz = 2
 .AnchorVert = 2
 .AlignVert = 2
END WITH

'--Create the preview sprite. It will be updated before it is drawn.
DIM preview as Slice Ptr
preview = NewSliceOfType(slSprite, preview_box)
'--Align the sprite to the bottom center of the containing box
WITH *preview
 .Y = -1
 .AnchorHoriz = 1
 .AlignHoriz = 1
 .AnchorVert = 2
 .AlignVert = 2
END WITH

'--Need a copy of the sprite to call frame_dissolved on
DIM preview_sprite as Frame ptr

'--dissolve_ticks is >= 0 while playing a dissolve; > dissolve_time while during lag period afterwards
DIM as integer dissolve_time, dissolve_type, dissolve_ticks
dissolve_ticks = -1

'--default starting menu
setactivemenu workmenu(), mainMenu(), state

DIM menudepth as integer = 0
DIM lastptr as integer = 0
DIM lasttop as integer = 0
DIM recindex as integer = 0

DIM rememberindex as integer = -1
DIM remember_bit as integer = -1
DIM show_name as integer = 0
DIM drawpreview as integer = YES

DIM tog as integer

'load data here
GOSUB EnLoadSub

'------------------------------------------------------------------------
'--main loop

setkeys YES
DO
 setwait 55
 setkeys YES
 tog = tog XOR 1
 IF keyval(scESC) > 1 THEN
  IF menudepth = 1 THEN
   GOSUB EnBackSub
   helpkey = "enemy"
   drawpreview = YES
  ELSE
   EXIT DO
  END IF
 END IF

 '--CTRL+BACKSPACE
 IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN
  cropafter recindex, gen(genMaxEnemy), 0, game + ".dt1", getbinsize(binDT1)
 END IF

 usemenu state

 IF workmenu(state.pt) = EnMenuChooseAct OR (keyval(scAlt) > 0 and NOT isStringField(menutype(workmenu(state.pt)))) THEN
  DIM lastindex as integer = recindex
  IF intgrabber_with_addset(recindex, 0, gen(genMaxEnemy), 32767, "enemy") THEN
   saveenemydata recbuf(), lastindex
   IF recindex > gen(genMaxEnemy) THEN
    '--adding a new set
    enemy_edit_add_new recbuf(), recindex, preview_box
    update_enemy_editor_for_elementals recbuf(), caption(), EnCapElemResist
    GOSUB EnUpdateMenu
   ELSE
    GOSUB EnLoadSub
   END IF
  END IF
 END IF

 IF keyval(scF1) > 1 THEN show_help helpkey

 IF keyval(scTab) > 1 THEN
  IF keyval(scLeftShift) > 0 OR keyval(scRightShift) > 0 THEN
   rememberindex = recindex
  ELSEIF rememberindex >= 0 AND rememberindex <= gen(genMaxEnemy) THEN
   saveenemydata recbuf(), recindex
   SWAP rememberindex, recindex
   GOSUB EnLoadSub
   show_name = 23
  END IF
 END IF

 IF enter_or_space() THEN
  SELECT CASE workmenu(state.pt)
   CASE EnMenuBackAct
    IF menudepth = 1 THEN
     GOSUB EnBackSub
     helpkey = "enemy"
     drawpreview = YES
    ELSE
     EXIT DO
    END IF
   CASE EnMenuAppearAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), appearMenu(), state
    helpkey = "enemy_appearance"
    GOSUB EnUpdateMenu
   CASE EnMenuRewardAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), rewardMenu(), state
    helpkey = "enemy_rewards"
    GOSUB EnUpdateMenu
   CASE EnMenuStatAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), statMenu(), state
    helpkey = "enemy_stats"
    GOSUB EnUpdateMenu
   CASE EnMenuSpawnAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), spawnMenu(), state
    helpkey = "enemy_spawning"
    drawpreview = NO
    GOSUB EnUpdateMenu
   CASE EnMenuAtkAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), atkMenu(), state
    helpkey = "enemy_attacks"
    drawpreview = NO
    GOSUB EnUpdateMenu
   CASE EnMenuElementalsAct
    GOSUB EnPushPtrSub
    setactivemenu workmenu(), elementalMenu(), state
    helpkey = "enemy_elementals"
    drawpreview = NO
    GOSUB EnUpdateMenu
   CASE EnMenuPal
    recbuf(EnDatPal) = pal16browse(recbuf(EnDatPal), recbuf(EnDatPicSize) + 1, recbuf(EnDatPic))
    GOSUB EnUpdateMenu
   CASE EnMenuDeathSFX
    IF recbuf(EnDatDeathSFX) >= 1 THEN playsfx recbuf(EnDatDeathSFX) - 1
    IF recbuf(EnDatDeathSFX) = 0 THEN playsfx gen(genDefaultDeathSFX) - 1
   CASE EnMenuBitsetAct
    editbitset recbuf(), EnDatBitset, UBOUND(ebit), ebit(), "enemy_bitsets", remember_bit
   CASE EnMenuDissolve, EnMenuDissolveTime
    IF recbuf(EnDatDissolve) THEN dissolve_type = recbuf(EnDatDissolve) - 1 ELSE dissolve_type = gen(genEnemyDissolve)
    dissolve_time = recbuf(EnDatDissolveTime) 
    IF dissolve_time = 0 THEN dissolve_time = default_dissolve_time(dissolve_type, preview_sprite->w, preview_sprite->h)
    dissolve_ticks = 0
   CASE EnMenuCursorOffset
    '--temporarily move the preview image
    SetSliceParent(preview, SliceTable.Root)
    preview->AnchorVert = 1
    preview->AlignVert = 1
    WITH sprite_sizes(recbuf(EnDatPicSize) + 1)
     recbuf(EnDatCursorX) += .size.x / 2 '--offset relative to the top middle
     xy_position_on_slice preview, recbuf(EnDatCursorX), recbuf(EnDatCursorY), "Targetting Cursor Offset", "xy_target_cursor"
     recbuf(EnDatCursorX) -= .size.x / 2
    END WITH
    '--move the preview image back how it was before
    SetSliceParent(preview, preview_box)
    preview->AnchorVert = 2
    preview->AlignVert = 2
  END SELECT
 END IF

 IF keyval(scAlt) = 0 or isStringField(menutype(workmenu(state.pt))) THEN 'not pressing ALT, or not allowed to
  IF editflexmenu(workmenu(state.pt), menutype(), menuoff(), menulimits(), recbuf(), caption(), min(), max()) THEN
   GOSUB EnUpdateMenu
  END IF
 END IF

 IF dissolve_ticks >= 0 THEN
  dissolve_ticks += 1
  IF dissolve_ticks > dissolve_time + 15 THEN
   dissolve_ticks = -1
   GOSUB EnUpdateMenu
  ELSE
   IF dissolve_ticks <= dissolve_time THEN
    SetSpriteToFrame preview, frame_dissolved(preview_sprite, dissolve_time, dissolve_ticks, dissolve_type), _
                     abs_pal_num(recbuf(EnDatPal), 1 + recbuf(EnDatPicSize), recbuf(EnDatPic))
   END IF
  END IF
 END IF
 'lag time after fading out, to give a more realistic preview
 preview->Visible = (dissolve_ticks <= dissolve_time)

 clearpage vpage
 IF drawpreview THEN
  DrawSlice preview_box, vpage
 END IF

 standardmenu dispmenu(), state, 0, 0, vpage
 draw_fullscreen_scrollbar state, , vpage
 IF keyval(scAlt) > 0 OR show_name > 0 THEN 'holding ALT or just pressed TAB
  show_name = large(0, show_name - 1)
  DIM tmpstr as string = readbadbinstring(recbuf(), EnDatName, 15, 0) & " " & recindex
  textcolor uilook(uiText), uilook(uiHighlight)
  printstr tmpstr, 320 - LEN(tmpstr) * 8, 0, vpage
 END IF

 setvispage vpage
 dowait
LOOP

'--save what we were last working on
saveenemydata recbuf(), recindex

resetsfx
clearallpages
DeleteSlice @preview_box
frame_unload @preview_sprite

EXIT SUB

'-----------------------------------------------------------------------

EnUpdateMenu:

'--in case new enemies have been added
max(EnLimSpawn) = gen(genMaxEnemy) + 1

'--in case the PicSize has changed
max(EnLimPic) = gen(genMaxEnemy1Pic + bound(recbuf(EnDatPicSize), 0, 2))

'--re-enforce bounds, as they might have just changed
enforceflexbounds menuoff(), menutype(), menulimits(), recbuf(), min(), max()

updateflexmenu state.pt, dispmenu(), workmenu(), state.last, menu(), menutype(), menuoff(), menulimits(), recbuf(), caption(), max(), recindex

'--stop sounds
resetsfx
'--update the picture and palette preview
frame_unload @preview_sprite
preview_sprite = frame_load(1 + recbuf(EnDatPicSize), recbuf(EnDatPic))
dissolve_ticks = -1
'--resets if dissolved
ChangeSpriteSlice preview, 1 + recbuf(EnDatPicSize), recbuf(EnDatPic), recbuf(EnDatPal), ,YES

RETRACE

'-----------------------------------------------------------------------

EnBackSub:
setactivemenu workmenu(), mainMenu(), state
menudepth = 0
state.pt = lastptr
state.top = lasttop
GOSUB EnUpdateMenu
RETRACE

'-----------------------------------------------------------------------

EnPushPtrSub:
lastptr = state.pt
lasttop = state.top
menudepth = 1
RETRACE

'-----------------------------------------------------------------------

EnLoadSub:
loadenemydata recbuf(), recindex
update_enemy_editor_for_elementals recbuf(), caption(), EnCapElemResist
GOSUB EnUpdateMenu
RETRACE

'-----------------------------------------------------------------------
END SUB

SUB enemy_edit_add_new (recbuf() as integer, byref recindex as integer, preview_box as Slice Ptr)
  DIM enemy as EnemyDef
  DIM menu(2) as string
  DIM enemytocopy as integer = 0
  DIM preview as Slice ptr = preview_box->FirstChild
  DIM state as MenuState
  state.last = UBOUND(menu)
  state.size = 24
  state.pt = 1

  state.need_update = YES
  setkeys
  DO
    setwait 55
    setkeys
    IF keyval(scESC) > 1 THEN  'cancel
      recindex -= 1
      EXIT SUB
    END IF
    IF keyval(scF1) > 1 THEN show_help "enemy_new"
    usemenu state
    IF state.pt = 2 THEN
      IF intgrabber(enemytocopy, 0, gen(genMaxEnemy)) THEN state.need_update = YES
    END IF
    IF state.need_update THEN
      state.need_update = NO
      loadenemydata recbuf(), enemytocopy
      loadenemydata enemy, enemytocopy
      ChangeSpriteSlice preview, 1 + enemy.size, enemy.pic, enemy.pal, , YES

      menu(0) = "Cancel"
      menu(1) = "New Blank Enemy"
      menu(2) = "Copy of Enemy " & enemytocopy & " " & enemy.name
    END IF
    IF enter_or_space() THEN
      SELECT CASE state.pt
        CASE 0 ' cancel
          recindex -= 1
        CASE 1 ' blank
          gen(genMaxEnemy) = recindex
          clearenemydata recbuf()
        CASE 2 ' copy
          gen(genMaxEnemy) = recindex
      END SELECT
      EXIT SUB
    END IF

    clearpage vpage
    standardmenu menu(), state, 0, 0, vpage
    IF state.pt = 2 THEN DrawSlice preview_box, vpage
    setvispage vpage
    dowait
  LOOP
END SUB


'--------------------------------- Formation Editor ----------------------------


SUB formation_editor
 DIM as integer csr, tog
 DIM menu(2) as string
 menu(0) = "Return to Main Menu"
 menu(1) = "Edit Individual Formations..."
 menu(2) = "Construct Formation Sets..."

 setkeys
 DO
  setwait 55
  setkeys
  tog = tog XOR 1
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "formation_main"
  usemenu csr, 0, 0, 2, 24
  IF enter_or_space() THEN
   IF csr = 0 THEN EXIT DO
   IF csr = 1 THEN individual_formation_editor
   IF csr = 2 THEN formation_set_editor
  END IF

  clearpage dpage
  standardmenu menu(), 2, 22, csr, 0, 0, 0, dpage

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB formation_set_editor
 DIM form as Formation
 DIM formset as FormationSet
 DIM set_id as integer = 1, form_id as integer
 DIM menu(23) as string
 DIM as integer tog, i
 DIM as GraphicPair egraphics(7)
 DIM as string ename(7)
 DIM state as MenuState
 state.last = UBOUND(menu)
 state.size = 24

 LoadFormationSet formset, set_id
 GOSUB lpreviewform
 setkeys
 DO
  setwait 55
  setkeys
  tog = tog XOR 1
  IF keyval(scESC) > 1 THEN
   SaveFormationSet formset, set_id
   EXIT DO
  END IF
  IF keyval(scF1) > 1 THEN show_help "formation_sets"
  IF usemenu(state) THEN GOSUB lpreviewform
  IF enter_or_space() THEN
   IF state.pt = 0 THEN
    SaveFormationSet formset, set_id
    EXIT DO
   END IF
  END IF
  IF state.pt = 1 THEN
   DIM remember_id as integer = set_id
   IF intgrabber(set_id, 1, 255) THEN
    SaveFormationSet formset, remember_id
    LoadFormationSet formset, set_id
   END IF
  END IF
  IF state.pt = 2 THEN intgrabber formset.frequency, 0, 200
  IF state.pt = 3 THEN tag_grabber formset.tag
  IF state.pt >= 4 THEN
   IF intgrabber(formset.formations(state.pt - 4), -1, gen(genMaxFormation)) THEN
    GOSUB lpreviewform
   END IF
  END IF
  IF state.pt >= 4 AND form_id >= 0 THEN
   copypage 2, dpage
   drawformsprites form, egraphics(), -1
  ELSE
   clearpage dpage
  END IF
  menu(0) = "Previous Menu"
  menu(1) = CHR(27) & "Formation Set " & set_id & CHR(26)
  menu(2) = "Battle Frequency: " & formset.frequency & " (" & step_estimate(formset.frequency, 60, 100, "-", " steps") & ")"
  menu(3) = tag_condition_caption(formset.tag, "Only if tag", "No tag check")
  FOR i as integer = 0 TO 19
   IF formset.formations(i) = -1 THEN
    menu(4 + i) = "Empty"
   ELSE
    menu(4 + i) = "Formation " & formset.formations(i)
   END IF
  NEXT i

  standardmenu menu(), state, 0, 0, dpage, YES  'edged=YES

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 FOR i as integer = 0 TO 7
  unload_sprite_and_pal egraphics(i)
 NEXT
 EXIT SUB

lpreviewform:
 IF state.pt >= 4 THEN
  '--have form selected
  form_id = formset.formations(state.pt - 4)
  IF form_id >= 0 THEN
   '--form not empty
   LoadFormation form, form_id
   loadmxs game + ".mxs", form.background, vpages(2)
   formpics(ename(), form, egraphics())
  END IF
 END IF
 RETRACE

END SUB

SUB individual_formation_editor ()
 DIM form_id as integer = 0
 DIM form as Formation
 DIM ename(7) as string
 DIM egraphics(7) as GraphicPair
 DIM as integer csr3, i, tog
 DIM as integer bgwait, bgctr

 LoadFormation form, form_id
 loadmxs game + ".mxs", form.background, vpages(2)
 formpics(ename(), form, egraphics())

 DIM menu(13) as string
 DIM state as MenuState
 state.pt = 0
 state.top = 0
 state.first = 0
 state.last = UBOUND(menu)
 state.size = 20

 DIM slot as integer = state.pt - 6

 setkeys
 DO
  setwait 55
  setkeys
  tog = tog XOR 1
  IF csr3 = 1 THEN
   '--enemy positioning mode
   IF keyval(scESC) > 1 OR enter_or_space() THEN setkeys: csr3 = 0
   IF keyval(scF1) > 1 THEN show_help "formation_editor_placement"
   DIM as integer thiswidth = 0, movpix
   movpix = 1 + (7 * SGN(keyval(scLeftShift) OR keyval(scRightShift)))
   WITH form.slots(slot)
    IF egraphics(slot).sprite THEN thiswidth = egraphics(slot).sprite->w
    IF keyval(scUp) > 0 AND .pos.y > 0 THEN .pos.y = .pos.y - movpix
    IF keyval(scDown) > 0 AND .pos.y < 199 - thiswidth THEN .pos.y = .pos.y + movpix
    IF keyval(scLeft) > 0 AND .pos.x > 0 THEN .pos.x = .pos.x - movpix
    IF keyval(scRight) > 0 AND .pos.x < 250 - thiswidth THEN .pos.x = .pos.x + movpix
   END WITH
  END IF
  IF csr3 = 0 THEN
   '--menu mode
   IF keyval(scESC) > 1 THEN
    EXIT DO
   END IF
   IF keyval(scF1) > 1 THEN show_help "formation_editor"
   IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN cropafter form_id, gen(genMaxFormation), 0, game + ".for", 80
   usemenu state
   slot = state.pt - 6

   IF enter_or_space() THEN
    IF state.pt = 0 THEN
     EXIT DO
    END IF
    IF state.pt = 5 THEN
     IF form.music >= 0 THEN playsongnum form.music
    END IF
    IF state.pt >= 6 THEN 'an enemy
     IF form.slots(slot).id >= 0 THEN csr3 = 1
    END IF
   END IF
   IF state.pt = 2 THEN
    IF intgrabber(form.background, 0, gen(genNumBackdrops) - 1) THEN
     loadmxs game + ".mxs", form.background, vpages(2)
     bgwait = 0
     bgctr = 0
    END IF
   END IF
   IF state.pt = 3 THEN
    'IF intgrabber(form.background_frames, 1, 50) THEN
    DIM temp as integer = form.background_frames - 1
    IF xintgrabber(temp, 2, 50) THEN
     IF form.background_frames = 1 THEN form.background_ticks = 8  'default to 8 ticks because 1 tick can be really painful
     form.background_frames = temp + 1
     IF bgctr >= form.background_frames THEN
      bgctr = 0
      loadmxs game + ".mxs", form.background, vpages(2)
     END IF
    END IF
   END IF
   IF state.pt = 4 THEN
    IF intgrabber(form.background_ticks, 0, 1000) THEN
     bgwait = 0
    END IF
   END IF
   IF state.pt = 5 THEN
    IF intgrabber(form.music, -2, gen(genMaxSong)) THEN
     music_stop
    END IF
   END IF
   IF state.pt = 1 THEN '---SELECT A DIFFERENT FORMATION
    DIM as integer remember_id = form_id
    IF intgrabber_with_addset(form_id, 0, gen(genMaxFormation), 32767, "formation") THEN
     SaveFormation form, remember_id
     IF form_id > gen(genMaxFormation) THEN
      gen(genMaxFormation) = form_id
      ClearFormation form
      form.music = gen(genBatMus) - 1
      SaveFormation form, form_id
     END IF
     LoadFormation form, form_id
     loadmxs game + ".mxs", form.background, vpages(2)
     formpics(ename(), form, egraphics())
     bgwait = 0
     bgctr = 0
    END IF
   END IF'--DONE SELECTING DIFFERENT FORMATION
   IF state.pt >= 6 THEN
    WITH form.slots(slot)
     DIM oldenemy as integer = .id
     IF intgrabber(.id, -1, gen(genMaxEnemy)) THEN
      'This would treat the x/y position as being the bottom middle of enemies, which makes much more
      'sense, but that would change where enemies of different sizes are spawned in slots in existing games
      'See the Plan for battle formation improvements
      '.pos.x += w(slot) \ 2
      '.pos.y += h(slot)
      formpics(ename(), form, egraphics())
      'default to middle of field
      IF oldenemy = -1 AND .pos.x = 0 AND .pos.y = 0 THEN
       .pos.x = 70
       .pos.y = 95
      END IF
      '.pos.x -= w(slot) \ 2
      '.pos.y -= h(slot)
     END IF
    END WITH
   END IF
  END IF

  IF form.background_frames > 1 AND form.background_ticks > 0 THEN
   bgwait = (bgwait + 1) MOD form.background_ticks
   IF bgwait = 0 THEN
    bgctr = loopvar(bgctr, 0, form.background_frames - 1, 1)
    loadmxs game + ".mxs", (form.background + bgctr) MOD gen(genNumBackdrops), vpages(2)
   END IF
  END IF
  copypage 2, dpage

  drawformsprites form, egraphics(), slot
  FOR i as integer = 0 TO 3
   edgeboxstyle 240 + i * 8, 75 + i * 22, 32, 40, 0, dpage, NO, YES
  NEXT i
  IF csr3 = 0 THEN
   menu(0) = "Previous Menu"
   menu(1) = CHR(27) + "Formation " & form_id & CHR(26)
   menu(2) = "Backdrop: " & form.background
   IF form.background_frames <= 1 THEN
    menu(3) = "Backdrop Animation: none"
    menu(4) = " Ticks per Backdrop Frame: -NA-"
   ELSE
    menu(3) = "Backdrop Animation: " & form.background_frames & " frames"
    menu(4) = " Ticks per Backdrop Frame: " & form.background_ticks
   END IF
   menu(5) = "Battle Music:"
   IF form.music = -2 THEN
     menu(5) &= " -same music as map-"
   ELSEIF form.music = -1 THEN
     menu(5) &= " -silence-"
   ELSEIF form.music >= 0 THEN
     menu(5) &= " " & form.music & " " & getsongname(form.music)
   END IF
   FOR i as integer = 0 TO 7
    menu(6 + i) = "Enemy:" + ename(i)
   NEXT i
   standardmenu menu(), state, 0, 0, dpage, YES
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP

 SaveFormation form, form_id
 music_stop
 FOR i as integer = 0 TO 7
  unload_sprite_and_pal egraphics(i)
 NEXT
END SUB

SUB formpics(ename() as string, form as Formation, egraphics() as GraphicPair)
 DIM enemy as EnemyDef
 FOR i as integer = 0 TO 7
  ename(i) = "-EMPTY-"
  unload_sprite_and_pal egraphics(i)
  IF form.slots(i).id >= 0 THEN
   loadenemydata enemy, form.slots(i).id
   WITH enemy
    ename(i) = form.slots(i).id & ":" & .name
    load_sprite_and_pal egraphics(i), 1 + bound(.size, 0, 2), .pic, .pal
   END WITH
  END IF
 NEXT i
END SUB

SUB drawformsprites(form as Formation, egraphics() as GraphicPair, byval slot as integer)
 DIM z(7) as integer, basey(7) as integer
 STATIC flash as integer
 flash = (flash + 1) MOD 256

 FOR i as integer = 0 TO 7
  IF egraphics(i).sprite THEN basey(i) = form.slots(i).pos.y + egraphics(i).sprite->h
 NEXT
 sort_integers_indices(z(), @basey(0))

 FOR i as integer = 0 TO 7
  DIM fslot as FormationSlot ptr = @form.slots(z(i))
  IF fslot->id >= 0 THEN
   WITH egraphics(z(i))
    frame_draw .sprite, .pal, fslot->pos.x, fslot->pos.y, , , dpage
    IF slot = z(i) THEN
     textcolor flash, 0
     printstr CHR(25), fslot->pos.x + .sprite->w \ 2 - 4, fslot->pos.y, dpage
    END IF
   END WITH
  END IF
 NEXT
END SUB

SUB itemdata
DIM a(dimbinsize(binITM)) as integer
DIM menu(20) as string
DIM bmenu(40) as string
DIM eqst(5) as string
DIM max(18) as integer
DIM min(18) as integer
DIM sbmax(11) as integer
DIM frame as integer
DIM item(maxMaxItems) as string
DIM info as string
DIM wep_img as GraphicPair 'This is only used in edititem
DIM box_preview as string = "" 'This is only used in edititem
'DIM ibitnames(-1 TO 59) as string
DIM imax as integer = 32

eqst(0) = "NEVER EQUIPPED"
eqst(1) = "Weapon"
FOR i as integer = 0 TO 3
 eqst(i + 2) = readglobalstring(25 + i, "Armor" & i+1)
NEXT i
FOR i as integer = 0 TO 1
 sbmax(i) = 9999
NEXT i
FOR i as integer = 2 TO 8
 sbmax(i) = 999
NEXT i
FOR i as integer = 9 TO 10
 sbmax(i) = 100
NEXT i
sbmax(11) = 10

DIM csr as integer = 0
DIM top as integer = -1
DIM pt as integer = 0
DIM tog as integer
DIM need_update as integer = NO
DIM ptr2 as integer
DIM caption as string
load_item_names item()
setkeys
DO
 setwait 55
 setkeys
 tog = tog XOR 1
 IF keyval(scESC) > 1 THEN EXIT DO
 IF keyval(scF1) > 1 THEN show_help "item_editor_pickitem"
 IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 AND csr >= 0 THEN
  cropafter csr, gen(genMaxItem), 0, game + ".itm", getbinsize(binITM)
  load_item_names item()
  load_special_tag_caches
 END IF
 usemenu csr, top, -1, gen(genMaxItem) + 1, 23
 intgrabber csr, -1, gen(genMaxItem) + 1
 IF enter_or_space() THEN
  IF csr = -1 THEN EXIT DO
  IF csr = gen(genMaxItem) + 1 THEN
   IF gen(genMaxItem) < maxMaxItems THEN
    gen(genMaxItem) += 1
    item_editor_init_new a()
    saveitemdata a(), csr
   END IF
  END IF
  IF csr <= gen(genMaxItem) THEN
   GOSUB edititem
   saveitemdata a(), csr
   loaditemdata a(), csr
   a(0) = LEN(item(csr))
   FOR o as integer = 1 TO a(0)
    a(o) = ASC(MID(item(csr), o, 1))
   NEXT o
   a(9) = LEN(info)
   FOR o as integer = 10 TO 9 + a(9)
    a(o) = ASC(MID(info, o - 9, 1))
   NEXT o
   saveitemdata a(), csr
  END IF
 END IF

 clearpage dpage
 FOR i as integer = top TO top + 23
  IF i <= gen(genMaxItem) + 1 THEN
   textcolor uilook(uiMenuItem), 0
   IF i = csr THEN textcolor uilook(uiSelectedItem + tog), 0
   SELECT CASE i
    CASE IS < 0
     caption = "Return to Main Menu"
    CASE IS > gen(genMaxItem)
     IF gen(genMaxItem) < maxMaxItems THEN
      caption = "Add a new item"
     ELSE
      caption = "No more items can be added"
      textcolor uilook(uiDisabledItem), 0
      IF i = csr THEN textcolor uilook(uiSelectedDisabled + tog), 0
     END IF
    CASE ELSE
     caption = i & " " & item(i)
   END SELECT
   printstr caption, 0, (i - top) * 8, dpage
  END IF
 NEXT i
 SWAP vpage, dpage
 setvispage vpage
 dowait
LOOP
EXIT SUB

edititem:
loaditemdata a(), csr
info = readbadbinstring(a(), 9, 35, 0)

menu(0) = "Back to Item Menu"
menu(18) = "Stat Bonuses..."
menu(19) = "Elemental Resists..."
'menu(19) = "Equipment Bits..."
menu(20) = "Who Can Equip?..."
max(3) = 32767
max(4) = gen(genMaxAttack)
max(5) = gen(genMaxAttack)
max(6) = 5
max(7) = gen(genMaxAttack)
max(8) = gen(genMaxAttack)
max(9) = gen(genMaxWeaponPic)
max(10) = 32767
min(10) = -1
max(11) = 2
max(12) = max_tag()
max(13) = max_tag()
max(14) = max_tag()
max(15) = max_tag()

loaditemdata a(), csr
generate_item_edit_menu menu(), a(), csr, pt, item(csr), info, eqst(), box_preview

IF wep_img.sprite THEN frame_unload @wep_img.sprite
IF wep_img.pal    THEN palette16_unload @wep_img.pal
wep_img.sprite = frame_load(5, a(52))
wep_img.pal    = palette16_load(a(53), 5, a(52))

need_update = NO

setkeys YES
DO
 setwait 55
 setkeys YES
 tog = tog XOR 1
 IF keyval(scESC) > 1 THEN EXIT DO
 IF keyval(scF1) > 1 THEN show_help "item_editor"
 usemenu pt, 0, 0, 20, 24
 frame = 0
 IF pt = 16 THEN frame = 1
 IF pt = 17 THEN frame = 0
 IF enter_or_space() THEN
  IF pt = 0 THEN EXIT DO
  IF a(49) > 0 THEN
   IF pt = 16 THEN
    xy_position_on_sprite wep_img, a(80), a(81), 0, 24, 24, "weapon handle position", "xy_weapon_handle"
    need_update = YES
   END IF
   IF pt = 17 THEN
    xy_position_on_sprite wep_img, a(78), a(79), 1, 24, 24, "weapon handle position", "xy_weapon_handle"
    need_update = YES
   END IF
   IF pt = 18 THEN
    GOSUB statbon
    need_update = YES
   END IF
   IF pt = 19 THEN
    item_editor_elementals a()
   END IF
   'IF pt = 19 THEN
   ' editbitset a(), 70, 23, ibitnames()
   ' need_update = YES
   'END IF
   IF pt = 20 THEN
    item_editor_equipbits a()
    need_update = YES
   END IF
  END IF
  IF pt = 10 THEN '--palette picker
   a(46 + (pt - 3)) = pal16browse(a(53), 5, a(52))
   need_update = YES
  END IF
 END IF
 SELECT CASE pt
  CASE 1
   strgrabber item(csr), 8
   menu(1) = "Name:" + item(csr)
  CASE 2
   strgrabber info, 36
   menu(2) = "Info:" + info
  CASE 3, 6, 9, 10
   IF intgrabber(a(46 + (pt - 3)), min(pt), max(pt)) THEN
    need_update = YES
   END IF
  CASE 4, 5, 7
   IF zintgrabber(a(46 + (pt - 3)), -1, max(pt)) THEN
    need_update = YES
   END IF
  CASE 8
   IF xintgrabber(a(46 + (pt - 3)), 0, max(pt), -1, gen(genMaxTextbox) * -1) THEN
    need_update = YES
   END IF
  CASE 11
   IF intgrabber(a(73), 0, 2) THEN
    need_update = YES
   END IF
  CASE 12 TO 15
   IF tag_grabber(a(74 + (pt - 12)), 0) THEN
    need_update = YES
    'Update cache
    itemtags(csr).have_tag = a(74)
    itemtags(csr).in_inventory_tag = a(75)
    itemtags(csr).is_equipped_tag = a(76)
    itemtags(csr).is_actively_equipped_tag = a(77)
   END IF
 END SELECT
 IF need_update THEN
  need_update = NO
  generate_item_edit_menu menu(), a(), csr, pt, item(csr), info, eqst(), box_preview
  IF wep_img.sprite THEN frame_unload @wep_img.sprite
  IF wep_img.pal    THEN palette16_unload @wep_img.pal
  wep_img.sprite = frame_load(5, a(52))
  wep_img.pal    = palette16_load(a(53), 5, a(52))
 END IF

 clearpage dpage
 FOR i as integer = 0 TO 20
  textcolor uilook(uiMenuItem), 0
  IF pt = i THEN textcolor uilook(uiSelectedItem + tog), 0
  IF (i >= 18 AND a(49) = 0) OR ((i = 16 OR i = 17) AND a(49) <> 1) THEN
   textcolor uilook(uiDisabledItem), 0
   IF pt = i THEN textcolor uilook(uiSelectedDisabled + tog), 0
  END IF
  printstr menu(i), 0, i * 8, dpage
 NEXT i
 IF a(49) = 1 THEN
  frame_draw wep_img.sprite + 1 - frame, wep_img.pal, 280, 160,,,dpage
  textcolor uilook(uiMenuItem), 0
  drawline 278 + a(78 + frame * 2),160 + a(79 + frame * 2),279 + a(78 + frame * 2), 160 + a(79 + frame * 2),14 + tog,dpage
  drawline 280 + a(78 + frame * 2),158 + a(79 + frame * 2),280 + a(78 + frame * 2), 159 + a(79 + frame * 2),14 + tog,dpage
  drawline 281 + a(78 + frame * 2),160 + a(79 + frame * 2),282 + a(78 + frame * 2), 160 + a(79 + frame * 2),14 + tog,dpage
  drawline 280 + a(78 + frame * 2),161 + a(79 + frame * 2),280 + a(78 + frame * 2), 162 + a(79 + frame * 2),14 + tog,dpage
 END IF
 edgeprint box_preview, 0, 191, uilook(uiText), dpage
 SWAP vpage, dpage
 setvispage vpage
 dowait
LOOP
IF wep_img.sprite THEN frame_unload @wep_img.sprite
IF wep_img.pal    THEN palette16_unload @wep_img.pal
RETRACE

statbon:
ptr2 = 0
setkeys
DO
 setwait 55
 setkeys
 tog = tog XOR 1
 IF keyval(scESC) > 1 THEN RETRACE
 IF keyval(scF1) > 1 THEN show_help "equipment_stat_bonuses"
 usemenu ptr2, 0, -1, 11, 24
 IF enter_or_space() THEN
  IF ptr2 = -1 THEN RETRACE
 END IF
 IF ptr2 >= 0 THEN
  intgrabber a(54 + ptr2), sbmax(ptr2) * -1, sbmax(ptr2)
 END IF

 clearpage dpage
 textcolor uilook(uiMenuItem), 0
 IF ptr2 = -1 THEN textcolor uilook(uiSelectedItem + tog), 0
 printstr "Previous Menu", 0, 0, dpage
 FOR i as integer = 0 TO 11
  textcolor uilook(uiMenuItem), 0
  IF ptr2 = i THEN textcolor uilook(uiSelectedItem + tog), 0
  printstr statnames(i) + " Bonus: " & a(54 + i), 0, 8 + i * 8, dpage
 NEXT i
 SWAP vpage, dpage
 setvispage vpage
 dowait
LOOP

END SUB


SUB generate_item_edit_menu (menu() as string, itembuf() as integer, csr as integer, pt as integer, item_name as string, info_string as string, equip_types() as string, byref box_preview as string)
 menu(1) = "Name:" & item_name
 menu(2) = "Info:" & info_string
 menu(3) = "Value: " & itembuf(46)
 menu(4) = "When used in battle: " & item_attack_name(itembuf(47))
 menu(5) = "When used as a Weapon: " & item_attack_name(itembuf(48))
 menu(6) = "Equippable as: " & equip_types(bound(itembuf(49), 0, 5))
 menu(7) = "Teach Spell: " & item_attack_name(itembuf(50))
 IF itembuf(51) >= 0 THEN
  menu(8) = "When used out of battle: " & item_attack_name(itembuf(51))
  box_preview = ""
 ELSE
  menu(8) = "When used out of battle: Text " & ABS(itembuf(51))
  box_preview = textbox_preview_line(ABS(itembuf(51)))
 END IF
 menu(9) = "Weapon Picture: " & itembuf(52)
 menu(10) = "Weapon Palette: " & defaultint(itembuf(53))
 IF itembuf(49) <> 1 THEN menu(9) = "Weapon Picture: N/A": menu(10) = "Weapon Palette: N/A"
 menu(11) = "Unlimited Use"
 IF itembuf(73) = 1 THEN menu(11) = "Consumed By Use"
 IF itembuf(73) = 2 THEN menu(11) = "Cannot be Sold/Dropped"
 menu(12) = "own item TAG " & itembuf(74) & " " & load_tag_name(itembuf(74))
 menu(13) = "is in inventory TAG " & itembuf(75) & " " & load_tag_name(itembuf(75))
 menu(14) = "is equipped TAG " & itembuf(76) & " " & load_tag_name(itembuf(76))
 menu(15) = "eqpt by active hero TAG " & itembuf(77) & " " & load_tag_name(itembuf(77))
 menu(16) = "Handle position A..."
 menu(17) = "Handle position B..."
 IF itembuf(49) <> 1 THEN
  menu(16) = menu(16) & " N/A"
  menu(17) = menu(17) & " N/A"
 END IF
END SUB

FUNCTION item_attack_name(n as integer) as string
 IF n <= 0 THEN RETURN "NOTHING"
 RETURN n - 1 & " " & readattackname(n - 1)
END FUNCTION

SUB load_item_names (item_strings() as string)
 DIM i as integer
 FOR i as integer = 0 TO gen(genMaxItem)
  item_strings(i) = load_item_name(i, YES, YES)
 NEXT i
END SUB

SUB handle_npc_def_delete (npc() as NPCType, byval id as integer, byref num_npc_defs as integer, npc_insts() as NPCInst)

 '--Count number of uses
 DIM as integer uses = 0
 FOR i as integer = 0 to 299
  IF npc_insts(i).id = id + 1 THEN uses += 1
 NEXT

 IF uses > 0 THEN
  IF yesno("There are " & uses & " copies of NPC ID " & id & " on the map. Are you sure you want to delete them?", NO, NO) = NO THEN EXIT SUB

  '--Delete instances of this ID
  FOR i as integer = 0 to UBOUND(npc_insts)
   IF npc_insts(i).id = id + 1 THEN npc_insts(i).id = 0
  NEXT
 END IF

 '--Wiping a definition clear, or completely deleting it?
 DIM as integer deleting = NO
 '--Can't delete ID 0; must always have at least one NPC
 IF id > 0 AND id = num_npc_defs - 1 THEN deleting = YES

 IF yesno(iif_string(uses, "Done. ", "") & "Really " & iif_string(deleting, "delete", "wipe clean") & " this NPC definition?", NO, NO) = NO THEN EXIT SUB

 IF deleting THEN
  num_npc_defs -= 1
 ELSE
  CleanNPCDefinition npc(id)
 END IF

END SUB

SUB npcdef (st as MapEditState, byval mapnum as integer, npc_img() as GraphicPair, gmap() as integer, zmap as ZoneMap)
'npc_img() should be of fixed size (0 TO max_npc_defs - 1), like st.npc_def(), with the actual number passed in st.num_npc_defs

DIM boxpreview(st.num_npc_defs - 1) as string
DIM as integer tog, i, top = 0, cur = 0, menumax, need_update_selected = NO

'--If there's room for more, add "Add new NPC" option to end
menumax = st.num_npc_defs - 1
IF st.num_npc_defs < max_npc_defs THEN menumax += 1

FOR i as integer = 0 TO st.num_npc_defs - 1
 boxpreview(i) = textbox_preview_line(st.npc_def(i).textbox)
NEXT i
setkeys
DO
 setwait 55
 setkeys
 tog = tog XOR 1
 IF keyval(scESC) > 1 THEN EXIT DO
 IF keyval(scF1) > 1 THEN show_help "pick_npc_to_edit"
 intgrabber cur, 0, menumax
 usemenu cur, top, 0, menumax, 7
 IF enter_or_space() THEN
  IF cur = st.num_npc_defs THEN
   '--Add new NPC option
   st.num_npc_defs += 1
   CleanNPCDefinition st.npc_def(st.num_npc_defs - 1)
  ELSE
   '--An NPC
   'First save NPCs so that we can correctly search for unused one-time use tags (see onetimetog)
   SaveNPCD_fixedlen maplumpname(mapnum, "n"), st.npc_def(), st.num_npc_defs
   edit_npc st.npc_def(cur), gmap(), zmap
  END IF
  need_update_selected = YES
 END IF
 IF keyval(scPlus) > 1 THEN
  '--Fast add button (for people who really want ID 134 for a task)
  st.num_npc_defs += 1
  CleanNPCDefinition st.npc_def(st.num_npc_defs - 1)
  cur = st.num_npc_defs - 1
  need_update_selected = YES
 END IF
 IF keyval(scDelete) > 1 THEN
  '--This updates st.num_npc_defs as needed, but not cur
  handle_npc_def_delete st.npc_def(), cur, st.num_npc_defs, st.npc_inst()
  IF cur > st.num_npc_defs - 1 THEN
   '--Deleted last NPC def
   frame_unload @npc_img(cur).sprite
   palette16_unload @npc_img(cur).pal
   cur = st.num_npc_defs - 1
  END IF
  need_update_selected = YES
 END IF

 IF need_update_selected THEN
  '--Note not all, or even any, of these updates will be required in a given case
  '--Re-load the picture and palette
  WITH npc_img(cur)
   IF .sprite THEN frame_unload(@.sprite)
   .sprite = frame_load(4, st.npc_def(cur).picture)
   IF .pal THEN palette16_unload(@.pal)
   .pal = palette16_load(st.npc_def(cur).palette, 4, st.npc_def(cur).picture)
  END WITH
  '--Update box preview line
  REDIM PRESERVE boxpreview(st.num_npc_defs - 1)
  boxpreview(cur) = textbox_preview_line(st.npc_def(cur).textbox)
  '--Update menumax
  menumax = st.num_npc_defs - 1
  IF st.num_npc_defs < max_npc_defs THEN menumax += 1

  need_update_selected = NO
 END IF

 clearpage dpage
 FOR i as integer = top TO top + 7
  IF i > menumax THEN EXIT FOR
  IF cur = i THEN edgebox 0, (i - top) * 25, 320, 22, uilook(uiDisabledItem), uilook(uiMenuItem), dpage
  textcolor uilook(uiMenuItem), 0
  IF cur = i THEN textcolor uilook(uiSelectedItem + tog), 0
  IF i > st.num_npc_defs - 1 THEN
   '--Add new NPC option
   printstr "Add new NPC", 0, ((i - top) * 25) + 5, dpage
  ELSE
   '--An NPC
   printstr "" & i, 0, ((i - top) * 25) + 5, dpage
   WITH npc_img(i)
    '--Down A frame
    frame_draw .sprite + 4, .pal, 32, (i - top) * 25, 1, -1, dpage
   END WITH
   textcolor uilook(uiMenuItem), uilook(uiHighlight)
   IF cur = i THEN textcolor uilook(uiText), uilook(uiHighlight)
   printstr boxpreview(i), 56, ((i - top) * 25) + 5, dpage
  END IF
 NEXT i
 SWAP vpage, dpage
 setvispage vpage
 dowait
LOOP

END SUB

'--Hero Editor stuff---------------------------------------------------

SUB hero_editor_load_hero (st as HeroEditState, her as HeroDef, byval hero_id as integer)
 loadherodata her, hero_id
 enforce_hero_data_limits her
 update_hero_preview_pics st, her
END SUB

SUB hero_editor
 DIM hero_id as integer = 0
 DIM her as HeroDef
 DIM st as HeroEditState
 WITH st
  .preview_walk_direction = 1
  .preview_steps = 0
  .preview_walk_pos.x = 0
  .preview_walk_pos.y = 0
  .previewframe = -1
 END WITH

 DIM menu(10) as string
 menu(0) = "Return to Main Menu"
 menu(1) = CHR(27) & "Pick Hero " & hero_id & CHR(26)
 menu(2) = "Name:"
 menu(3) = "Appearance and Misc..."
 menu(4) = "Edit Stats..."
 menu(5) = "Edit Spell Lists..."
 menu(6) = "Edit Battle Menu..."
 menu(7) = "Bitsets..."
 menu(8) = "Elemental Resistances..."
 menu(9) = "Hero Tags..."
 menu(10) = "Equipment..."

 DIM mstate as MenuState
 WITH mstate
  .pt = 1
  .size = 22
  .last = UBOUND(menu)
 END WITH

 DIM hbit(-1 TO 26) as string
 'Lots of obsolete elemental bits
 hbit(24) = "Rename when added to party"
 hbit(25) = "Permit renaming on status screen"
 hbit(26) = "Do not show spell lists if empty"

 hero_editor_load_hero st, her, hero_id

 setkeys YES
 DO
  setwait 55
  setkeys YES
  animate_hero_preview st
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_editor"
  IF keyval(scCtrl) > 0 AND keyval(scBackspace) > 0 THEN
   cropafter hero_id, gen(genMaxHero), -1, game + ".dt0", getbinsize(binDT0)
   load_special_tag_caches
  END IF
  usemenu mstate
  IF enter_or_space() THEN
   SELECT CASE mstate.pt
    CASE 0: EXIT DO
    CASE 3: hero_editor_appearance st, her
    CASE 4: hero_editor_stats_menu her
    CASE 5: hero_editor_spell_lists_toplevel her
    CASE 6: hero_editor_battle_menu her
    CASE 7: editbitset her.bits(), 0, 26, hbit()
    CASE 8: hero_editor_elementals her
    CASE 9: hero_editor_tags hero_id, her
    CASE 10: hero_editor_equipment_list hero_id, her
   END SELECT
  END IF

  IF mstate.pt = 1 THEN
   DIM remem_hero_id as integer = hero_id
   IF intgrabber(hero_id, 0, gen(genMaxHero), scLeftCaret, scRightCaret) THEN
    saveherodata her, remem_hero_id
    hero_editor_load_hero st, her, hero_id
   END IF
   IF keyval(scLeft) > 1 AND hero_id > 0 THEN
    saveherodata her, hero_id
    hero_id -= 1
    hero_editor_load_hero st, her, hero_id
   END IF
   IF keyval(scRight) > 1 AND hero_id < 59 THEN
    saveherodata her, hero_id
    hero_id += 1
    IF needaddset(hero_id, gen(genMaxHero), "hero") THEN
     gen(genMaxHero) -= 1  'Incremented by both needaddset and hero_editor_add_new
     hero_editor_add_new st, her, hero_id
     saveherodata her, hero_id
    END IF
    hero_editor_load_hero st, her, hero_id
   END IF
  END IF
  IF mstate.pt = 2 THEN
   strgrabber her.name, 16
  END IF

  menu(1) = CHR(27) & "Pick Hero " & hero_id & CHR(26)
  menu(2) = "Name:" & her.name

  '--Draw screen
  clearpage dpage
  standardmenu menu(), mstate, 0, 0, dpage

  draw_hero_preview st, her
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 saveherodata her, hero_id
 clear_hero_preview_pics st
END SUB

SUB hero_editor_add_new (st as HeroEditState, her as HeroDef, byref recindex as integer)
  DIM menu(2) as string
  DIM herotocopy as integer = 0
  DIM state as MenuState
  state.last = UBOUND(menu)
  state.size = 24
  state.pt = 1

  state.need_update = YES
  setkeys
  DO
    setwait 55
    animate_hero_preview st
    setkeys
    IF keyval(scESC) > 1 THEN  'cancel
      recindex -= 1
      EXIT DO
    END IF
    IF keyval(scF1) > 1 THEN show_help "hero_new"
    usemenu state
    IF state.pt = 2 THEN
      IF intgrabber(herotocopy, 0, gen(genMaxHero)) THEN state.need_update = YES
    END IF
    IF state.need_update THEN
      state.need_update = NO
      loadherodata her, herotocopy
      update_hero_preview_pics st, her
      menu(0) = "Cancel"
      menu(1) = "New Blank Hero"
      menu(2) = "Copy of Hero " & herotocopy & " " & her.name
    END IF
    IF enter_or_space() THEN
      SELECT CASE state.pt
        CASE 0 ' cancel
          recindex -= 1
        CASE 1 ' blank
          gen(genMaxHero) += 1
          ClearHeroData her
        CASE 2 ' copy
          gen(genMaxHero) += 1
      END SELECT
      EXIT DO
    END IF

    clearpage dpage
    standardmenu menu(), state, 0, 0, dpage
    IF state.pt = 2 THEN
      draw_hero_preview st, her
    END IF
    setvispage dpage
    dowait
  LOOP
END SUB

SUB hero_editor_stats_menu (her as HeroDef)
 DIM i as integer
 DIM activemenu as integer = 0  '0 or 1 indicating left or rightmenu

 '.unselectable attribute only used from leftmenu
 DIM as BasicMenuItem vector leftmenu, rightmenu
 v_new leftmenu, 3 + 12
 v_new rightmenu, 3 + 12
 leftmenu[0].text = "Previous Menu"
 leftmenu[1].unselectable = YES
 leftmenu[2].text = "LEVEL ZERO"
 leftmenu[2].unselectable = YES
 leftmenu[2].col = uilook(uiDescription)
 rightmenu[2].text = "LEVEL " & gen(genMaxLevel)
 rightmenu[2].col = uilook(uiDescription)

 DIM mstate as MenuState
 WITH mstate
  .last = v_len(leftmenu) - 1
  .size = 22
  .need_update = YES
 END WITH

 DIM min(11) as integer, max(11) as integer
 FOR i as integer = 0 TO UBOUND(min)
  min(i) = 0
  max(i) = 999
 NEXT
 max(statHP) = 9999
 max(statMP) = 9999
 max(statFocus) = 100
 max(statHitX) = 10

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_stats"

  usemenu mstate, leftmenu
  DIM statnum as integer = mstate.pt - 3

  IF enter_or_space() AND mstate.pt = 0 THEN EXIT DO
  IF statnum < 0 THEN
   activemenu = 0
  ELSE
   IF keyval(scLeft) > 1 OR keyval(scRight) > 1 THEN activemenu XOR= 1
   IF activemenu = 0 THEN
    IF intgrabber(her.Lev0.sta(statnum), min(statnum), max(statnum), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
   ELSE
    IF intgrabber(her.LevMax.sta(statnum), min(statnum), max(statnum), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
   END IF
  END IF

  IF mstate.need_update THEN
   mstate.need_update = NO
   FOR i as integer = 0 TO UBOUND(her.Lev0.sta)
    leftmenu[3 + i].text = statnames(i) & " " & her.Lev0.sta(i)
    rightmenu[3 + i].text = statnames(i) & " " & her.LevMax.sta(i)
   NEXT i
  END IF

  '--Draw screen
  clearpage dpage
  'Note that exactly one of the following two standardmenus should pass toggle=NO
  mstate.active = (activemenu = 0)
  standardmenu leftmenu, mstate, 8, 0, dpage, , (activemenu = 0)  'active=(...)
  mstate.active = (activemenu = 1)
  standardmenu rightmenu, mstate, 160, 0, dpage, , (activemenu = 1)  'active=(...)

  IF statnum >= 0 THEN  '--Draw graph
   textcolor uilook(uiMenuItem), 0
   printstr statnames(statnum), 310 - LEN(statnames(statnum)) * 8, 180, dpage
   DIM as integer stepper, n0, nMax, ordinate
   stepper = CEIL(gen(genMaxLevel) / 25)
   FOR i as integer = 0 TO gen(genMaxLevel) STEP stepper
    n0 = her.Lev0.sta(statnum)
    nMax = her.LevMax.sta(statnum)
    ordinate = atlevel(i, n0, nMax) * (100 / max(statnum))
    rectangle 290 + (i / stepper), 176 - ordinate, 1, ordinate + 1, uilook(uiMenuItem), dpage
   NEXT i

   'Experimental stuff
   IF keyval(scLeftShift) > 1 THEN
    debug "0=" & atlevel(0, n0, nMax) & " max(" & gen(genMaxLevel) & ")=" & atlevel(gen(genMaxLevel), n0, nMax) & " 99=" & atlevel(99, n0, nMax)
   END IF
   IF keyval(scF7) > 1 AND gen(genMaxLevel) > 1 THEN gen(genMaxLevel) -= 1 : debug "gen(genMaxLevel)=" & gen(genMaxLevel)
   IF keyval(scF8) > 1 THEN gen(genMaxLevel) += 1 : debug "gen(genMaxLevel)=" & gen(genMaxLevel)
  END IF

  IF statnum = statSpeed THEN
   textcolor uilook(uiDescription), 0
   printstr "Lev0:  1 turn every " & speed_estimate(her.Lev0.spd), 0, 182, dpage
   printstr "Lev" & gen(genMaxLevel) & ": 1 turn every " & speed_estimate(her.LevMax.spd), 0, 190, dpage
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 v_free leftmenu
 v_free rightmenu
END SUB

SUB hero_editor_spell_lists_toplevel (her as HeroDef)
 DIM spell_list_types(3) as string
 spell_list_types(0) = ""
 spell_list_types(1) = "Level-MP (FF1 Style)"
 spell_list_types(2) = "Random Effects"

 DIM menu(4) as string
 menu(0) = "Previous Menu"

 DIM mstate as MenuState
 WITH mstate
  .pt = 0
  .first = 0
  .last = UBOUND(menu)
  .size = 24
 END WITH

 DIM hint as string
 
 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_spell_menu_types"
  usemenu mstate
  DIM listnum as integer = mstate.pt - 1
  IF mstate.pt >= 1 THEN
   strgrabber her.list_name(listnum), 10
  END IF
  IF keyval(scEnter) > 1 THEN
   IF mstate.pt = 0 THEN EXIT DO
   IF mstate.pt >= 1 AND mstate.pt <= 4 THEN
    IF LEN(her.list_name(listnum)) > 0 THEN
     hero_editor_edit_spell_list her, listnum
    END IF
   END IF
  END IF

  FOR i as integer = 0 TO 3
   menu(1 + i) =  "Spell list " & i & ": " & her.list_name(i)
  NEXT i
  clearpage dpage
  standardmenu menu(), mstate, 0, 0, dpage
  IF listnum >= 0 THEN
   edgeprint spell_list_types(her.list_type(listnum)), 0, 180, uilook(uiSelectedDisabled), dpage
   IF LEN(her.list_name(listnum)) > 0 THEN
    hint = "Press ENTER to edit spells in this list."
   ELSE
    hint = "Type a name for this spell list."
   END IF
   edgeprint hint, 0, 190, uilook(uiDisabledItem), dpage
  END IF
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB hero_editor_edit_spell_list (her as HeroDef, byval listnum as integer)

 DIM menu(2) as string
 DIM spellnames(23) as string
 DIM col as integer
 DIM bgcol as integer

 DIM root as Slice Ptr
 root = NewSliceOfType(slRoot)
 WITH *root
  .Fill = YES
 END WITH
 DIM spellbox as Slice Ptr
 spellbox = NewSliceOfType(slRectangle, root)
 WITH *spellbox
  .x = 6
  .y = 34
  .width = 308
  .height = 80
  ChangeRectangleSlice spellbox, 1
 END WITH
 DIM infobox as Slice Ptr
 RefreshSliceScreenPos root
 infobox = NewSliceOfType(slText, root)
 ChangeTextSlice infobox, "", , , YES ' make wrapping
 WITH *infobox
  .Width = root->Width
  .AlignVert = 2
  .AnchorVert = 2
 END WITH
 
 DIM info as string

 DIM mstate as MenuState
 WITH mstate
  .first = -3
  .pt = -3
  .top = -3
  .last = UBOUND(spellnames)
  .need_update = YES
 END WITH
 menu(0) = "Previous Menu..."

 setkeys YES
 DO
  setwait 55
  setkeys YES
  IF keyval(scF1) > 1 THEN show_help "hero_spells"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  IF mstate.pt >= 0 THEN
   IF usemenu(mstate, scLeft, scRight) THEN
    mstate.need_update = YES
   ELSE
    IF keyval(scUp) > 1 THEN
     mstate.pt -= 3
     IF mstate.pt < 0 THEN mstate.pt = -1
     mstate.need_update = YES
    ELSEIF keyval(scDown) > 1 THEN
     mstate.pt += 3
     IF mstate.pt > mstate.last THEN mstate.pt = mstate.first
     mstate.need_update = YES
    END IF
   END IF
  ELSE
   IF usemenu(mstate) THEN mstate.need_update = YES
  END IF

  IF enter_or_space() THEN
   IF mstate.pt = -3 THEN
    EXIT DO
   END IF
   IF mstate.pt >= 0 THEN
    hero_editor_edit_spell_list_slot her.spell_lists(listnum, mstate.pt)
    mstate.need_update = YES
   END IF
  END IF
  
  IF mstate.pt = -2 THEN
   IF strgrabber(her.list_name(listnum), 10) THEN mstate.need_update = YES
  END IF

  IF mstate.pt = -1 THEN
   IF intgrabber(her.list_type(listnum), 0, 2) THEN mstate.need_update = YES
  END IF

  IF mstate.pt >= 0 THEN
   IF zintgrabber(her.spell_lists(listnum, mstate.pt).attack, -1, gen(genMaxAttack), scLeftCaret, scRightCaret) THEN mstate.need_update = YES
  END IF

  IF mstate.need_update THEN
   menu(1) = "Spell list name: " & her.list_name(listnum)
   menu(2) = "List Type: "
   SELECT CASE her.list_type(listnum)
    CASE 0: menu(2) &= "Normal"
    CASE 1: menu(2) &= "Use Level-MP (FF1-Style)"
    CASE 2: menu(2) &= "Random Effects"
    CASE ELSE: menu(2) &= "???Unknown " & her.list_type(listnum)
   END SELECT
   FOR i as integer = 0 TO UBOUND(spellnames)
    WITH her.spell_lists(listnum, i)
     IF .attack > 0 THEN
      spellnames(i) = readattackname(.attack - 1)
     ELSE
      spellnames(i) = ""
     END IF
     spellnames(i) = rpad(spellnames(i), " ",  10)
    END WITH
   NEXT i
   info = ""
   IF mstate.pt >= 0 THEN
    WITH her.spell_lists(listnum, mstate.pt)
     IF .learned > 0 THEN
      info &= "Learned at level " & (.learned - 1) & !"\n"
     ELSE
      info &= "Learned from an item" & !"\n"
     END IF
    END WITH
    IF her.list_type(listnum) = 1 THEN
     info &= "1 point of level " & INT(mstate.pt / 3) + 1 & " " & readglobalstring(160, "Level MP", 20)
    END IF
   END IF
   ChangeTextSlice infobox, info
   mstate.need_update = NO
  END IF

  '--Draw screen
  mstate.tog XOR= 1
  clearpage dpage
  drawslice root, dpage
  FOR i as integer = 0 TO 2
   col = uiLook(uiMenuItem)
   IF mstate.pt + 3 = i THEN col = uiLook(uiSelectedItem) + mstate.tog
   edgeprint menu(i), 0, 10 * i, col, dpage
  NEXT i
  FOR i as integer = 0 TO mstate.last
   col = uiLook(uiMenuItem)
   bgcol = 0
   IF mstate.pt = i THEN
    col = uiLook(uiSelectedItem) + mstate.tog
    bgcol = uilook(uiHighlight)
   END IF
   textcolor col, bgcol
   printstr spellnames(i), 12 + (i MOD 3) * 104, 40 + (i \ 3) * 8, dpage
  NEXT i

  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 
 DeleteSlice @root
END SUB

SUB hero_editor_edit_spell_list_slot (slot as SpellList)

 DIM menu as MenuDef
 ClearMenuData menu
 append_menu_item menu, "Previous menu..."
 append_menu_item menu, "Attack:"
 append_menu_item menu, "Learned at level:"
 append_menu_item menu, "Learned from an item:"
 menu.align = -1
 
 DIM st as MenuState
 init_menu_state st, menu
 st.pt = 1
 st.need_update = YES
 st.active = YES

 DIM cap as string = ""

 DIM holdscreen as integer
 holdscreen = allocatepage
 copypage vpage, holdscreen

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "hero_spells_slot"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  usemenu st

  SELECT CASE st.pt
   CASE 0:
    IF enter_or_space() THEN
     EXIT DO
    END IF
   CASE 1:
    IF zintgrabber(slot.attack, -1, gen(genMaxAttack)) THEN st.need_update = YES
   CASE 2:
    IF zintgrabber(slot.learned, -1, 99) THEN st.need_update = YES
   CASE 3:
    IF enter_or_space() ORELSE keyval(scLeft) > 1 ORELSE keyval(scRight) > 1 THEN
     IF slot.learned > 0 THEN
      slot.learned = 0
     ELSE
      slot.learned = 1
     END IF
     st.need_update = YES
    END IF
  END SELECT
  
  IF st.need_update THEN
   cap = ""
   IF slot.attack > 0 THEN cap = (slot.attack - 1) & " " & readattackname(slot.attack - 1)
   menu.items[1]->caption = "Attack: " & cap

   cap = ""
   IF slot.learned > 0 THEN cap = STR(slot.learned - 1)
   menu.items[2]->caption = "Learned at level: " & cap

   cap = "NO"
   IF slot.learned = 0 THEN cap = "YES"
   menu.items[3]->caption = "Learned from an item: " & cap
   
   st.need_update = NO
   copypage holdscreen, vpage
  END IF

  draw_menu menu, st, vpage
  setvispage vpage
  
  dowait
 LOOP

 freepage holdscreen
 setkeys
 ClearMenuData menu
END SUB

SUB hero_editor_battle_menu (her as HeroDef)
 DIM menu(-1 TO 5) as string
 menu(-1) = "Previous menu..."
 
 DIM st as MenuState
 st.pt = -1
 st.top = -1
 st.first = -1
 st.last = UBOUND(menu)
 st.need_update = YES
 
 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scF1) > 1 THEN show_help "hero_battle_menu_edit"
  IF keyval(scEsc) > 1 THEN EXIT DO
  
  usemenu st
  
  IF st.need_update THEN
   FOR i as integer = 0 TO 5
   NEXT i
   st.need_update = NO
  END IF
  
  clearpage dpage
  standardmenu menu(), st, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  
  dowait
 LOOP
 
END SUB

SUB update_hero_tags_menu (byref hero as HeroDef, menu() as string)
 WITH hero
  menu(0) = "Previous Menu: "
  menu(1) = "have hero: " + tag_condition_caption(.have_tag, "set tag", "NONE", "Not usable")
  menu(2) = "is alive: " + tag_condition_caption(.alive_tag, "set tag", "NONE", "Not usable")
  menu(3) = "is leader: " + tag_condition_caption(.leader_tag, "set tag", "NONE", "Not usable")
  menu(4) = "is in party now: " + tag_condition_caption(.active_tag, "set tag", "NONE", "Not usable")
 END WITH
END SUB

SUB hero_editor_tags (byval hero_id as integer, byref hero as HeroDef)
 DIM menu(5) as string
 DIM st as MenuState
 st.need_update = YES
 st.last = 4
 st.size = 24

 setkeys
 DO
  setwait 55
  setkeys
  IF keyval(scESC) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_tags"
  usemenu st
  WITH hero
   SELECT CASE st.pt
    CASE 0
     IF enter_or_space() THEN EXIT DO
    CASE 1
     tag_grabber .have_tag, 0
    CASE 2
     tag_grabber .alive_tag, 0
    CASE 3
     tag_grabber .leader_tag, 0
    CASE 4
     tag_grabber .active_tag, 0
   END SELECT

   'Update cached hero tags
   herotags(hero_id).have_tag = .have_tag
   herotags(hero_id).alive_tag = .alive_tag
   herotags(hero_id).leader_tag = .leader_tag
   herotags(hero_id).active_tag = .active_tag
  END WITH

  clearpage dpage
  update_hero_tags_menu hero, menu()
  standardmenu menu(), st, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

'This is not complete; it exists just to prevent crashes due to data
'corruption (eg. bug 871)
SUB enforce_hero_data_limits(her as HeroDef)
 clamp_value her.sprite, 0, gen(genMaxHeroPic), "hero sprite"
 clamp_value her.sprite_pal, -1, gen(genMaxPal), "hero sprite pal"
 clamp_value her.walk_sprite, 0, gen(genMaxNPCPic), "hero walkabout sprite"
 clamp_value her.walk_sprite_pal, -1, gen(genMaxPal), "hero walkabout sprite pal"
 clamp_value her.portrait, -1, gen(genMaxPortrait), "hero portrait"
 clamp_value her.portrait_pal, -1, gen(genMaxPal), "hero portrait pal"
 FOR i as integer = 0 TO 3
  clamp_value her.list_type(i), 0, 2, "hero spell list " & i & " type"
 NEXT i
END SUB

SUB update_hero_appearance_menu(byref st as HeroEditState, menu() as string, her as HeroDef)
 menu(1) = "Battle Picture: " & her.sprite
 menu(2) = "Battle Palette: " & defaultint(her.sprite_pal)
 menu(3) = "Walkabout Picture: " & her.walk_sprite
 menu(4) = "Walkabout Palette: " & defaultint(her.walk_sprite_pal)
 menu(5) = "Base Level: " & her.def_level
 IF her.def_level < 0 THEN menu(5) = "Base Level: Party Average"
 menu(6) = "Default Weapon: " & load_item_name(her.def_weapon, 0, 1)
 menu(7) = "Max Name Length: " & zero_default(her.max_name_len)
 menu(8) = "Hand position A..."
 menu(9) = "Hand position B..."
 menu(10) = "Portrait Picture: " & defaultint(her.portrait, "None")
 menu(11) = "Portrait Palette: " & defaultint(her.portrait_pal)
 update_hero_preview_pics st, her
 st.changed = NO
END SUB

SUB update_hero_preview_pics(byref st as HeroEditState, her as HeroDef)
 clear_hero_preview_pics st
 WITH st
  .battle.sprite    = frame_load(0, her.sprite)
  .battle.pal       = palette16_load(her.sprite_pal, 0, her.sprite)
  .walkabout.sprite = frame_load(4, her.walk_sprite)
  .walkabout.pal    = palette16_load(her.walk_sprite_pal, 4, her.walk_sprite)
  IF her.portrait >= 0 THEN
   .portrait.sprite = frame_load(8, her.portrait)
   .portrait.pal    = palette16_load(her.portrait_pal, 8, her.portrait)
  END IF
 END WITH
END SUB

SUB clear_hero_preview_pics(byref st as HeroEditState)
 WITH st
  IF .battle.sprite    THEN frame_unload    @.battle.sprite
  IF .battle.pal       THEN palette16_unload @.battle.pal
  IF .walkabout.sprite THEN frame_unload    @.walkabout.sprite
  IF .walkabout.pal    THEN palette16_unload @.walkabout.pal
  IF .portrait.sprite  THEN frame_unload    @.portrait.sprite
  IF .portrait.pal     THEN palette16_unload @.portrait.pal
 END WITH
END SUB

SUB draw_hero_preview(st as HeroEditState, her as HeroDef)
 STATIC tog as integer
 tog = tog XOR 1
 
 DIM frame as integer
 IF st.previewframe <> -1 THEN
  frame = st.previewframe + 2
 ELSE
  frame = tog
 END IF
 frame_draw st.battle.sprite + frame, st.battle.pal, 250, 25,,,dpage
 frame = st.preview_walk_direction * 2 + tog
 frame_draw st.walkabout.sprite + frame, st.walkabout.pal, 230 + st.preview_walk_pos.x, 5 + st.preview_walk_pos.y,,,dpage
 DIM hand as XYPair
 IF st.previewframe <> -1 THEN
  hand.x = her.hand_pos(st.previewframe).x
  hand.y = her.hand_pos(st.previewframe).y
  drawline 248 + hand.x,25 + hand.y,249 + hand.x, 25 + hand.y,14 + tog, dpage
  drawline 250 + hand.x,23 + hand.y,250 + hand.x, 24 + hand.y,14 + tog, dpage
  drawline 251 + hand.x,25 + hand.y,252 + hand.x, 25 + hand.y,14 + tog, dpage
  drawline 250 + hand.x,26 + hand.y,250 + hand.x, 27 + hand.y,14 + tog, dpage
 END IF
 IF st.portrait.sprite THEN frame_draw st.portrait.sprite, st.portrait.pal, 240, 110,,,dpage
END SUB

SUB animate_hero_preview(byref st as HeroEditState)
 WITH st
  .preview_steps += 1
  IF .preview_steps >= 15 THEN
   .preview_steps = 0
   .preview_walk_direction = loopvar(.preview_walk_direction, 0, 3, 1)
  END IF
  IF .preview_walk_direction = 0 THEN .preview_walk_pos.y -= 4
  IF .preview_walk_direction = 1 THEN .preview_walk_pos.x += 4
  IF .preview_walk_direction = 2 THEN .preview_walk_pos.y += 4
  IF .preview_walk_direction = 3 THEN .preview_walk_pos.x -= 4
 END WITH
END SUB

SUB hero_editor_appearance(byref st as HeroEditState, byref her as HeroDef)
 
 DIM menu(11) as string
 DIM min(11) as integer
 DIM max(11) as integer
 menu(0) = "Previous Menu"
 min(1) = 0: max(1) = gen(genMaxHeroPic)
 min(2) = -1: max(2) = 32767
 min(3) = 0: max(3) = gen(genMaxNPCPic)
 min(4) = -1: max(4) = 32767
 min(5) = -1: max(5) = 99
 min(6) = 0: max(6) = gen(genMaxItem)
 min(7) = 0: max(7) = 16
 min(8) = -100:max(8) = 100
 min(9) = -100:max(9) = 100
 min(10) = -1:max(10) = gen(genMaxPortrait)
 min(11) = -1:max(11) = 32767

 DIM state as MenuState
 WITH state
  .pt = 0
  .last = UBOUND(menu)
  .size = 24
 END WITH

 update_hero_appearance_menu st, menu(), her
 st.changed = NO
 setkeys
 DO
  setwait 55
  setkeys
  state.tog = state.tog XOR 1
  animate_hero_preview st
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_appearance"
  usemenu state
  st.previewframe = -1
  IF state.pt = 8 THEN st.previewframe = 0
  IF state.pt = 9 THEN st.previewframe = 1
  IF enter_or_space() AND state.pt = 0 THEN EXIT DO
  IF state.pt > 0 THEN
   SELECT CASE state.pt
    CASE 1
     IF intgrabber(her.sprite, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 2
     IF intgrabber(her.sprite_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 3
     IF intgrabber(her.walk_sprite, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 4
     IF intgrabber(her.walk_sprite_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 5
     IF intgrabber(her.def_level, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 6
     IF intgrabber(her.def_weapon, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 7
     IF intgrabber(her.max_name_len, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 10
     IF intgrabber(her.portrait, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
    CASE 11
     IF intgrabber(her.portrait_pal, min(state.pt), max(state.pt)) THEN
      st.changed = YES
     END IF
   END SELECT
   IF enter_or_space() THEN
    SELECT CASE state.pt
     CASE 2
      her.sprite_pal = pal16browse(her.sprite_pal, 0, her.sprite)
     CASE 4
      her.walk_sprite_pal = pal16browse(her.walk_sprite_pal, 4, her.walk_sprite)
     CASE 8
      xy_position_on_sprite st.battle, her.hand_pos(0).x, her.hand_pos(0).y, 2, 32, 40, "hand position (for weapon)", "xy_hero_hand"
     CASE 9
      xy_position_on_sprite st.battle, her.hand_pos(1).x, her.hand_pos(1).y, 3, 32, 40, "hand position (for weapon)", "xy_hero_hand"
     CASE 11
      her.portrait_pal = pal16browse(her.portrait_pal, 8, her.portrait)
    END SELECT
    st.changed = YES
   END IF
  END IF

  IF st.changed THEN update_hero_appearance_menu st, menu(), her

  clearpage dpage
  standardmenu menu(), state, 8, 0, dpage
  draw_hero_preview st, her
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
 st.previewframe = -1
END SUB

SUB hero_editor_equipment_list (byval hero_id as integer, byref her as HeroDef)
 DIM menu(5) as string
 DIM state as MenuState
 WITH state
  .last = 5
  .size = 22
 END WITH
 menu(0) = "Previous menu"
 menu(1) = her.name & "'s " & readglobalstring(38, "Weapon", 10) & " items"
 FOR i as integer = 0 TO 3
  menu(2+i) = her.name & "'s " & readglobalstring(25+i, "Armor " & (1+i), 10) & " items"
 NEXT i
 
 setkeys
 DO
  setwait 55
  setkeys
  state.tog = state.tog XOR 1
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help "hero_equipment"
  usemenu state
  IF enter_or_space() THEN
   IF state.pt = 0 THEN
    EXIT DO
   ELSE
    hero_editor_equipbits hero_id, state.pt
   END IF
  END IF
  clearpage dpage
  standardmenu menu(), state, 0, 0, dpage
  SWAP vpage, dpage
  setvispage vpage
  dowait
 LOOP
END SUB

SUB hero_editor_equipbits (byval hero_id as integer, byval equip_type as integer)
 '--equip_type is 0 for none (which would be silly) 1 for weapons and 2-5 for armor
 DIM tempbits(gen(genMaxItem) \ 16 + 1) as integer 
 DIM itemname(gen(genMaxItem)) as string
 DIM item_id(gen(genMaxItem)) as integer
 DIM itembuf(dimbinsize(binITM)) as integer
 DIM nextbit as integer = 0
 FOR i as integer = 0 TO gen(genMaxItem)
  loaditemdata itembuf(), i
  IF itembuf(49) = equip_type THEN
   itemname(nextbit) = readitemname(i)
   item_id(nextbit) = i
   setbit tempbits(), 0, nextbit, readbit(itembuf(), 66, hero_id)
   nextbit += 1
  END IF
 NEXT i
 editbitset tempbits(), 0, nextbit-1, itemname()
 FOR i as integer = 0 TO nextbit-1
  loaditemdata itembuf(), item_id(i)
  setbit itembuf(), 66, hero_id, readbit(tempbits(), 0, i)
  saveitemdata itembuf(), item_id(i)
 NEXT i
END SUB

'This elemental resistance editor is shared by the hero and item editors
SUB common_elementals_editor(elementals() as single, helpfile as string, byval showsign as integer = 0)
 DIM elementnames() as string
 getelementnames elementnames()
 DIM float_reprs(gen(genNumElements) - 1) as string
 DIM menu(1 + gen(genNumElements) - 1) as string
 DIM st as MenuState
 st.last = UBOUND(menu)
 st.size = 22
 st.need_update = YES

 FOR i as integer = 0 TO gen(genNumElements) - 1
  float_reprs(i) = format_percent(elementals(i))
  elementnames(i) = rpad(elementnames(i), " ", 15)
 NEXT

 DO
  setwait 55
  setkeys YES
  IF keyval(scEsc) > 1 THEN EXIT DO
  IF keyval(scF1) > 1 THEN show_help helpfile
  IF st.pt = 0 THEN
   IF enter_or_space() THEN EXIT DO
  ELSE
   IF percent_grabber(elementals(st.pt - 1), float_reprs(st.pt - 1), -1000, 1000) THEN st.need_update = YES
  END IF
  usemenu st

  IF st.need_update THEN
   st.need_update = NO
   menu(0) = "Previous Menu"
   FOR i as integer = 0 TO gen(genNumElements) - 1
    menu(i + 1) = "Damage from " + elementnames(i) + ": "
    IF showsign THEN
     'positive values get explicit + prefix
     IF LEFT(float_reprs(i), 1) <> "-" THEN menu(i + 1) += "+"
    END IF
    menu(i + 1) += float_reprs(i)
   NEXT
  END IF

  clearpage vpage
  standardmenu menu(), st, 0, 0, vpage
  setvispage vpage
  dowait
 LOOP
 setkeys
END SUB

SUB hero_editor_elementals(byref her as HeroDef)
 common_elementals_editor her.elementals(), "hero_elementals"
END SUB

'--Item Editor stuff---------------------------------------------------

SUB item_editor_equipbits(itembuf() as integer)
 DIM ibit(-1 TO maxMaxHero) as string
 FOR i as integer = 0 TO gen(genMaxHero)
  ibit(i) = "Equipable by " & getheroname(i)
 NEXT i
 editbitset itembuf(), 66, gen(genMaxHero), ibit()
END SUB

SUB item_editor_elementals(itembuf() as integer)
 DIM elementals(gen(genNumElements) - 1) as single
 FOR i as integer = 0 TO gen(genNumElements) - 1
  elementals(i) = DeSerSingle(itembuf(), 82 + i * 2)
  IF gen(genEquipMergeFormula) = 2 THEN  'additive merging
   elementals(i) -= 1.0
  END IF
 NEXT
 common_elementals_editor elementals(), "item_elementals", (gen(genEquipMergeFormula) = 2)
 FOR i as integer = 0 TO gen(genNumElements) - 1
  IF gen(genEquipMergeFormula) = 2 THEN  'additive merging
   elementals(i) += 1.0
  END IF
  SerSingle itembuf(), 82 + i * 2, elementals(i)
 NEXT
END SUB

SUB item_editor_init_new(itembuf() as integer)
 flusharray itembuf(), dimbinsize(binITM), 0
 FOR i as integer = 0 TO maxElements - 1
  SerSingle itembuf(), 82 + i * 2, 1.0
 NEXT i
END SUB
